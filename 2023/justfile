# Use `just work day-01 part1` to work on the specific binary for a specific day's problems
work day part:
    cargo watch -w {{day}} -x "check -p {{day}}" -s "just test {{day}} {{part}}" -s "just clippy {{day}}" -s "just bench {{day}} {{part}}" --ignore '*.bench.txt'
clippy day:
    cargo clippy -p {{day}}
test day part:
    cargo nextest run -p {{day}} --lib {{part}}
bench-all:
    cargo bench -q > benchmarks.txt
bench day part:
    cargo bench --bench {{day}} {{part}} >> {{day}}/{{part}}.bench.txt
flamegraph day part:
    cargo flamegraph --profile flamegraph --root --package {{day}} --bin {{part}} -o profiling/flamegraphs/{{day}}_{{part}}.svg
dhat day part:
    cargo run --profile dhat --features dhat-heap --package {{day}} --bin {{part}}
init day:
    cargo generate --path ./daily-template --name {{day}}
run day part:
    cargo run --package {{day}} --bin {{part}}
